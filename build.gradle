plugins {
    id 'nebula.project' version '7.0.5'
    id 'nebula.release' version '14.0.2'
    id 'nebula.kotlin' version '1.3.61'
    id 'nebula.maven-publish' version '14.1.1'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'au.com.console'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        events("passed", "skipped", "failed")
        setExceptionFormat("full")
    }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'com.natpryce:hamkrest:1.2.3.0'
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:2.0.9"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.0.0'
    testRuntimeOnly "org.spekframework.spek2:spek-runner-junit5:2.0.9"
}

tasks.bintrayUpload.dependsOn tasks.check

gradle.taskGraph.whenReady { graph ->
    tasks.bintrayUpload.onlyIf {
        graph.hasTask(':final') || graph.hasTask(':candidate')
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['nebula']
    pkg {
        repo = 'kotlin'
        userOrg = 'consoleau'
        name = 'kassava'
        licenses = ['Apache-2.0']
        labels = ['kotlin']
        websiteUrl = "https://github.com/consoleau/${project.name}"
        issueTrackerUrl = "https://github.com/consoleau/${project.name}/issues"
        vcsUrl = "https://github.com/consoleau/${project.name}.git"
    }
}
